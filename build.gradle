plugins {
    id 'java'
    id 'idea'
    id 'antlr'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'raylras.zenls'
ext.moduleName = 'raylras.zenls'

java {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.antlr:antlr4-runtime:4.11.1'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.19.0'
    implementation 'org.tomlj:tomlj:1.1.0'

    antlr 'org.antlr:antlr4:4.11.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
}

sourceSets {
    generated {
        java.srcDir 'generated-src/antlr/main'
    }
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'raylras.zen.Main'
    }
    dependencies {
        include(dependency('org.antlr:antlr4-runtime'))
        include(dependency('org.eclipse.lsp4j:org.eclipse.lsp4j'))
        include(dependency('org.eclipse.lsp4j:org.eclipse.lsp4j.generator'))
        include(dependency('org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc'))
        include(dependency('com.google.code.gson:gson'))
        include(dependency('org.tomlj:tomlj'))
    }
    exclude '**/*.java', 'META-INF/**', '**/*.html'
    minimize()
}

task copyArtifacts(type: Copy) {
    dependsOn 'shadowJar'
    from "${buildDir}/libs/${project.name}-${project.version}-all.jar"
    into './vscode-extension'
}

generateGrammarSource {
    maxHeapSize = '64m'
    arguments += ['-visitor', '-listener', '-long-messages', '-package', 'raylras.zen.cst']
    outputDirectory = new File(buildDir.toString() + '/generated-src/antlr/main/raylras/zen/cst')
}

jar.enabled = false
build.finalizedBy(copyArtifacts)
