plugins {
    id 'java'
    id 'idea'
    id 'antlr'
}

group 'raylras.zenls'
ext.moduleName = 'raylras.zenls'

java {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
    mavenCentral()
}

dependencies {
    antlr 'org.antlr:antlr4:4.13.0'
    implementation 'org.antlr:antlr4-runtime:4.13.0'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.20.1'
    implementation('com.github.briandilley.jsonrpc4j:jsonrpc4j:1.6')
    implementation('com.fasterxml.jackson.core:jackson-databind:2.12.7.1')
    implementation('com.fasterxml.jackson.core:jackson-core:2.10.2')
    implementation('com.fasterxml.jackson.core:jackson-annotations:2.10.2')
    implementation('org.slf4j:slf4j-api:1.7.30')
    implementation('ch.qos.logback:logback-core:1.4.8')
    implementation('ch.qos.logback:logback-classic:1.4.8')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
}

test {
    useJUnitPlatform()
}

sourceSets {
    generated {
        java.srcDir 'generated-src/antlr/main'
    }
}

generateGrammarSource {
    maxHeapSize = '64m'
    arguments += ['-visitor', '-listener', '-long-messages', '-package', 'raylras.zen.code.parser']
    outputDirectory = new File(buildDir.toString() + '/generated-src/antlr/main/raylras/zen/code/parser')
}

task deleteOldLibs(type: Delete) {
    delete './vscode-extension/libs'
}

task copyArtifact(type: Copy) {
    from "${buildDir}/libs/"
    into './vscode-extension/libs'
    include "${project.name}-${project.version}.jar"
}

task copyDependencies(type: Copy) {
    from project.configurations.runtimeClasspath
    into './vscode-extension/libs'
    include "antlr4-runtime-*.jar"
    include "org.eclipse.lsp4j-*.jar"
    include "org.eclipse.lsp4j.jsonrpc-*.jar"
    include "org.eclipse.lsp4j.generator-*.jar"
    include "gson-*.jar"
    include 'jsonrpc4j-*.jar'
    include 'jackson-databind-*.jar'
    include 'jackson-core-*.jar'
    include 'jackson-annotations-*.jar'
    include 'slf4j-api-*.jar'
//    include 'logback-core-*.jar'
//    include 'logback-classic-*.jar'
}

build.finalizedBy(deleteOldLibs, copyArtifact, copyDependencies)
